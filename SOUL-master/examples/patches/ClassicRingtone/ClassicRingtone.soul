/*
    == SOUL example code ==
    == Author: Jules ==
*/

/// Title: The "Hello World" of audio demo code
///
/// This example plays the first few notes of Gran Vals by Francisco Tarrega,
/// using the simplest possible sine-wave oscillator.

processor ClassicRingtone
{
    output stream float out;

    void run()
    {
        int[] pitches    = (76, 74, 66, 68, 73, 71, 62, 64, 71, 69, 61, 64, 69);
        int[] durations  = ( 1,  1,  2,  2,  1,  1,  2,  2,  1,  1,  2,  2,  4);

        for (wrap<pitches.size> i)
            playNote (pitches[i], durations[i]);
    }

    float sinewavePhase;

    void playNote (int pitch, int lengthInQuarterNotes)
    {
        let samplesPerQuarterNote = int (processor.frequency / 7);

        let noteFrequency  = soul::noteNumberToFrequency (pitch);
        let noteLength     = samplesPerQuarterNote * lengthInQuarterNotes;
        let phaseIncrement = float (noteFrequency * twoPi * processor.period);

        loop (noteLength)
        {
            out << 0.1f * sin (sinewavePhase);
            sinewavePhase = addModulo2Pi (sinewavePhase, phaseIncrement);
            advance();
        }
    }
}
